%locations
%define parse.error verbose

%{
#include <stdio.h>
#include <stdlib.h>
#include <string>
#include "ast.hpp"
// using namespace std;

extern int yylex(void);
extern int yyparse(void);
extern void yyrestart(FILE*);

void yyerror(const char *str);

extern char* yytext;
extern int yylineno;
extern int errorLexFlag;
extern int errorSyntaxFlag;

ProgramAST* Program;

// extern int yyparse(yyFlexLexer* yyflex);
%}

%union
{
    int int_val;
    float float_val;
    char char_val;
    std::string* string_val;

    std::string* name;
    std::string* op; 
    std::string* Str;

    ast_list* AST_list;
    st_ast_list* ST_AST_list;
    index_list* Index_list;
    ExprAST* exprAST;
    FunctionAST* functionAST;
    TypeAST* typeAST;
    PrototypeAST* prototypeAST;
    BodyAST* bodyAST;
    VariableExprAST* variableexprAST;
    DecExprAST* decexprAST;
    DecListAST* declistAST;
    STDecListAST* stdeclistAST;
    ArrayDecAST* arraydecAST;
    ArrayEleAST* arrayeleAST;
}

%token <Str> COLON COMMA LC RC FUNCSPEC SEMI STRUCT 
             IF WHILE FOR TYPE ELSE ELIF IN RANGE
             DEF RETURN MAIN VAR STMT SELF
%token <name> ID
%token <int_val> INT
%token <float_val> FLOAT
%token <string_val> STRING

// %type <> Program ExtDefList ExtDef ExtDecList   //  High-level Definitions
// %type <> Specifier StructSpecifier OptTag Tag   //  Specifiers
// %type <> VarDec FunDec VarList ParamDec         //  Declarators
// %type <> CompSt StmtList Stmt                   //  Statements
// %type <> DefList Def Dec DecList                //  Local Definitions
// %type <> Exp Args                               //  Expressions
%type <AST_list> ExtDefList ExtDecList VarList DefList StmtList DecList Args IndexList 
%type <ST_AST_list> StructDecList StructExtDecList
%type <exprAST> ExtDef ReturnStmt Exp Stmt Dec ELIFStmt ExtDec StructExtDec
%type <functionAST> MainDef
%type <typeAST> Specifier
%type <prototypeAST> FunDec MainFunDec MemberFuncDec
%type <bodyAST> CompSt
%type <variableexprAST> VarDec 
%type <arraydecAST> ArrayDec ExtArrayDec
%type <decexprAST> ParamDec 
%type <declistAST> Def
%type <Index_list> DecIndexList ExtDecIndexList

%right <op> ASSIGNOP
%left <op> OR
%left <op> AND
%left <op> RELOP
%left <op> ADD SUB
%left <op> MUL DIV
%right <op> NOT
%left <op> DOT
%left <op> LB RB
%left <op> LP RP
%nonassoc LOWER_THAN_ELSE
%nonassoc ELIF
%nonassoc ELSE

%start Program

%%

// High-level Definitions
Program:            ExtDefList                              { Program = new ProgramAST(std::move(*$1)); }
    ; 
ExtDefList:         ExtDef ExtDefList                       { $$ = $2; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |               MainDef                                 { $$ = new ast_list();
                                                              $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |                                                       { $$ = new ast_list(); } 
    ; 
ExtDef:             ExtDecList SEMI                         { $$ = new GlobalDecListAST(std::move(*$1)); $$->setLineno(yylineno); }
    /* |               Specifier SEMI                          {  } */
    |               DEF FunDec COLON CompSt                 { $$ = new FunctionAST($2, $4); $$->setLineno(yylineno); }
    |               STRUCT ID COLON LC StructExtDecList RC SEMI   { 
                    $$ = new StructAST($2, std::move(*$5)); 
                    $$->setLineno(yylineno); }
    ; 
MainDef:            DEF MainFunDec COLON CompSt             { $$ = new FunctionAST($2, $4); $$->setLineno(yylineno); }
    ;
ExtDecList:         ExtDec                                  { $$ = new ast_list(); $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |               ExtDec COMMA ExtDecList                 { $$ = $3; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    ;
StructExtDecList:   StructExtDec StructExtDecList           { $$ = $2; $$->push_back($1); }
    |               StructExtDec                            { $$ = new st_ast_list(); $$->push_back($1); }
StructExtDec:       StructDecList SEMI                          { $$ = new STDecListAST(std::move(*$1)); $$->setLineno(yylineno); }
    |               MemberFuncDec COLON CompSt              { $$ = new FunctionAST($1, $3); $$->setLineno(yylineno);}
MemberFuncDec:      DEF ID LP SELF RP FUNCSPEC Specifier    { 
    auto* self_type_ptr = new TypeAST("undefined", 0); 
    auto* self_var_ptr = new VariableExprAST("self");
    auto* self_ptr = new DecExprAST(self_var_ptr, self_type_ptr, 0);
    auto* tmp = new ast_list(); 
    tmp->push_back(std::unique_ptr<ExprAST>(self_ptr)); 
    $$ = new PrototypeAST($7, *$2, std::move(*tmp), "undefined"); 
}
    |               DEF ID LP SELF COMMA VarList RP FUNCSPEC Specifier { 
        auto self_type_ptr = new TypeAST("undefined", 0); 
        auto self_var_ptr = new VariableExprAST("self");
        auto self_ptr = new DecExprAST(self_var_ptr, self_type_ptr, 0);
        $6->push_back(std::unique_ptr<ExprAST>(self_ptr));
        $$ = new PrototypeAST($9, *$2, std::move(*$6), "undefined"); 
    }
StructDecList:         ExtDec                                  
    { 
        auto* tmp = new st_ast_list(); 
        tmp->push_back(($1));
        $$ = tmp;
    }
    |               ExtDec COMMA StructDecList                 { $$ = $3; $$->push_back(($1)); }
    ; 
ExtDec:             VarDec COLON Specifier                  { $$ = new DecExprAST($1, $3, 1); $$->setLineno(yylineno); }
    |               ExtArrayDec COLON Specifier             { $$ = new DecExprAST($1, $3, 1); $$->setLineno(yylineno); }
    ;
// Specifiers
Specifier:          TYPE                                    { $$ = new TypeAST(*$1); $$->setLineno(yylineno); }
    |               STRUCT ID                               { $$ = new TypeAST(*$2, 0);}
    ; 
// Global Array declaration
ExtArrayDec:           ID LB RB                                { $$ = new ArrayDecAST(*$1); $$->setLineno(yylineno); }
    |                  ID ExtDecIndexList                      { $$ = new ArrayDecAST(*$1, std::move(*$2)); $$->setLineno(yylineno); }
    ;
ExtDecIndexList:       LB INT RB                               { $$ = new index_list(); $$->push_back($2); }
    |                  LB INT RB ExtDecIndexList               { $$ = $4; $$->push_back($2); }
    ;
// Declarators
VarDec:             ID                                      { $$ = new VariableExprAST(*$1); $$->setLineno(yylineno); }
    ;
ArrayDec:           ID LB RB                                { $$ = new ArrayDecAST(*$1); $$->setLineno(yylineno); }
    |               ID DecIndexList                         { $$ = new ArrayDecAST(*$1, std::move(*$2)); $$->setLineno(yylineno); }
    ;
DecIndexList:       LB INT RB                               { $$ = new index_list(); $$->push_back($2); }
    /* |               LB INT RB DecIndexList                  { $$ = $4; $$->push_back($2); } */
    ;
FunDec:             ID LP VarList RP FUNCSPEC Specifier     { $$ = new PrototypeAST($6, *$1, std::move(*$3)); $$->setLineno(yylineno); }
    |               ID LP RP FUNCSPEC Specifier             { ast_list* void_args; 
                                                              void_args = new ast_list(); 
                                                              $$ = new PrototypeAST($5, *$1, std::move(*void_args)); $$->setLineno(yylineno); }
    ; 
MainFunDec:         MAIN LP RP FUNCSPEC Specifier           { ast_list* void_args; 
                                                              void_args = new ast_list(); 
                                                              $$ = new PrototypeAST($5, std::string("main"), std::move(*void_args)); $$->setLineno(yylineno); }
    ; 
VarList:            ParamDec COMMA VarList                  { $$ = $3; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |               ParamDec                                { $$ = new ast_list(); $$->push_back(std::unique_ptr<ExprAST>($1)); }
    ; 
ParamDec:           VarDec COLON Specifier                  { $$ = new DecExprAST($1, $3, 0); $$->setLineno(yylineno); }
    |               ArrayDec COLON Specifier                { $$ = new DecExprAST($1, $3, 0); $$->setLineno(yylineno); }
    ; 
// Statements
CompSt:             LC StmtList RC         { $$ = new BodyAST(std::move(*$2)); $$->setLineno(yylineno); }
    |               LC StmtList ReturnStmt RC { $$ = new BodyAST(std::move(*$2), $3); $$->setLineno(yylineno); }
    ; 
StmtList:           Stmt StmtList                           { $$ = $2; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |                                                       { $$ = new ast_list(); }
    ; 
Stmt:               Exp SEMI                                { $$ = $1; $$->setLineno(yylineno); }
    |               DecList SEMI                            { $$ = new DecListAST(std::move(*$1)); $$->setLineno(yylineno); }
    |               CompSt                                  { $$ = $1; $$->setLineno(yylineno); }   
    |               IF LP Exp RP COLON Stmt %prec LOWER_THAN_ELSE               { $$ = new IfExprAST($3, $6); $$->setLineno(yylineno); }
    |               IF LP Exp RP COLON Stmt ELIFStmt %prec LOWER_THAN_ELSE      { $$ = new IfExprAST($3, $6, $7); $$->setLineno(yylineno); }
    |               IF LP Exp RP COLON Stmt ELSE COLON Stmt                     { $$ = new IfExprAST($3, $6, $9); $$->setLineno(yylineno); }
    |               WHILE LP Exp RP COLON Stmt                                      { $$ = new WhileExprAST($3, $6); $$->setLineno(yylineno); }
    |               FOR VarDec IN RANGE LP Exp COMMA Exp COMMA Exp RP COLON Stmt    { $$ = new ForExprAST($2, $6, $8, $10, $13); $$->setLineno(yylineno); }
    |               FOR VarDec IN RANGE LP Exp COMMA Exp RP COLON Stmt              { $$ = new ForExprAST($2, $6, $8, $11); $$->setLineno(yylineno); } 
    ; 
ELIFStmt:           ELIF LP Exp RP COLON Stmt ELIFStmt %prec LOWER_THAN_ELSE { $$ = new IfExprAST($3, $6, $7); $$->setLineno(yylineno); }
    |               ELIF LP Exp RP COLON Stmt %prec LOWER_THAN_ELSE { $$ = new IfExprAST($3, $6); $$->setLineno(yylineno); }
    |               ELIF LP Exp RP COLON Stmt ELSE COLON Stmt { $$ = new IfExprAST($3, $6, $9); $$->setLineno(yylineno); }
    ;
ReturnStmt:         RETURN Exp SEMI                         { $$ = $2; $$->setLineno(yylineno); }
    |               RETURN SEMI                             { $$ = new VoidExprAST(); $$->setLineno(yylineno); }
    ;
// Local Definitions
/* DefList:            Def DefList                             { $$ = $2; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |                                                       { $$ = new ast_list(); }
    ;      */
Def:                DecList SEMI                            { $$ = new DecListAST(std::move(*$1)); $$->setLineno(yylineno); }
    ; 
DecList:            Dec                                     { $$ = new ast_list(); $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |               Dec COMMA DecList                       { $$ = $3; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    ; 
Dec:                VarDec COLON Specifier                  { $$ = new DecExprAST($1, $3, 0); $$->setLineno(yylineno); }
    |               VarDec COLON Specifier ASSIGNOP Exp     { DecExprAST* var;
                                                              var = new DecExprAST($1, $3, 0); 
                                                              $$ = new AssignExprAST(var, $5); $$->setLineno(yylineno); }
    |               ArrayDec COLON Specifier                { $$ = new DecExprAST($1, $3, 0); $$->setLineno(yylineno); }
    ; 
//7.1.7 Expressions
Exp:                Exp ASSIGNOP Exp                        { $$ = new AssignExprAST($1, $3); $$->setLineno(yylineno); }
    |               Exp AND Exp                             { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               Exp OR Exp                              { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               Exp RELOP Exp                           { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               Exp ADD Exp                             { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               Exp SUB Exp                             { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               Exp MUL Exp                             { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               Exp DIV Exp                             { $$ = new BinaryExprAST(*$2, $1, $3); $$->setLineno(yylineno); }
    |               LP Exp RP                               { $$ = $2; $$->setLineno(yylineno); }
    |               SUB Exp                                 { $$ = new BinaryExprAST(*$1, new IntExprAST(0), $2); $$->setLineno(yylineno); }
    |               NOT Exp                                 { $$ = new BinaryExprAST(*$1, new VoidExprAST(), $2); $$->setLineno(yylineno); }
    |               ID LP Args RP                           { $$ = new CallExprAST(*$1, std::move(*$3)); $$->setLineno(yylineno); }
    |               ID LP RP                                { ast_list* void_args; 
                                                              void_args = new ast_list();  
                                                              $$ = new CallExprAST(*$1, std::move(*void_args)); $$->setLineno(yylineno); }
    |               ID IndexList                            { $$ = new ArrayEleAST(*$1, std::move(*$2)); $$->setLineno(yylineno); }
    |               ID LB RB                                { $$ = new ArrayDecAST(*$1); $$->setLineno(yylineno); }
    |               ID DOT ID                              { $$ = new StructEleAST(*$1, *$3); $$->setLineno(yylineno); } 
    |               SELF DOT ID                            { $$ = new StructEleAST("self", *$3); $$->setLineno(yylineno); }
    |               ID DOT ID LP Args RP                   { 
        $5->push_back(std::move(std::make_unique<VariableExprAST>(*$1))); 
        auto funcname = *$3; $$ = new CallExprAST(funcname, std::move(*$5), 1); }
    |               ID DOT ID LP RP {
        auto tmp = new ast_list();
        tmp->push_back(std::move(std::make_unique<VariableExprAST>(*$1))); 
        auto funcname = *$3; $$ = new CallExprAST(funcname, std::move(*tmp), 1); }
    |               ID                                      { $$ = new VariableExprAST(*$1); $$->setLineno(yylineno); }
    |               INT                                     { $$ = new IntExprAST($1); $$->setLineno(yylineno); }
    |               FLOAT                                   { $$ = new FloatExprAST($1); $$->setLineno(yylineno); }
    |               STRING                                  { $$ = new StringExprAST(*$1); $$->setLineno(yylineno); }
    ; 
Args :              Exp COMMA Args                          { $$ = $3; $$->push_back(std::unique_ptr<ExprAST>($1)); }
    |               Exp                                     { $$ = new ast_list(); $$->push_back(std::unique_ptr<ExprAST>($1)); }
    ; 
IndexList:          LB Exp RB                               { $$ = new ast_list(); $$->push_back(std::unique_ptr<ExprAST>($2)); }
    |               LB Exp RB IndexList                     { $$ = $4; $$->push_back(std::unique_ptr<ExprAST>($2)); }
    ;
%%

/* void yyerror(char* msg){
    fprintf(stderr, "[Parser]Error at line %d: %s.\n %s\n", yylineno, msg, yytext);
} */
